-- trans2

specs:		modal
defuncts:       three four five six seven eight drawS
fovars:		X Y

axioms:

states == [1,11] & labels == [a,b] & atoms == [A,B,C] &

(1,a) -> 1<+>2 & (2,a) -> 3 & (3,a) -> 1<+>4 & (4,a) -> 4 & (1,a) -> 5 & 
(5,a) -> 6 & (1,b) -> 5 & (4,b) -> 5 & (5,b) -> 6 & 3 -> 5 & 

A -> branch[2,3,4,11] & 
B -> branch[2,3,4] & 
C -> branch[2,3,4] & 

(A,a) -> 3 & 
(A,b) -> branch[4,6] &
(B,a) -> 3 & 
(B,b) -> 4 &
(C,a) -> 3 & 
(C,b) -> branch[4,6] &

three == mu X.([a]#(A\/X)) &		
four  == nu X.([a]#(A/\X)) &
five  == nu X.(([a]#X)/\six) &
six   == mu X.(A\/([a]#X)) &
seven == nu X.(mu Y.([a]#((A/\X)\/Y))) &
eight == nu X.([a]#X) &

drawS == wtree $ fun(dark green$x,green$text$x,
		     red$x,text$x,
		     [],text[],
		     x,red$text$x)

terms:

evalR$child[b] <+>  		    --> [(1,[5]),(5,[6]),(4,[5])]	
eval$three <+>	        	    --> [11,2,5,6,4]	
eval$not$three <+>		    --> [1,3]	
eval$four <+>			    --> [11,6,4]	
eval$A <+>			    --> [11,2,3,4]	
eval$B <+>			    --> [2,3,4]	
eval$valueL(A)$a <+>		    --> [3]
eval$valueL(B)$b <+>		    --> [4]
evalO$outL(3)$a <+>  		    --> [A,B,C]
evalO$outL(4)$b <+>  		    --> [A,B,C]	
map(eval)[five,six,seven,eight] <+> --> [[11,4..6],[11,2..6],[11,4..6],states]
traces(1)(6) --> 1-a-2-a-3-5-a-6 <+> 1-a-2-a-3-5-b-6 <+> 
             --  1-a-2-a-3-a-4-b-5-a-6 <+> 1-a-2-a-3-a-4-b-5-b-6 <+> 
             --  1-a-5-a-6 <+> 1-a-5-b-6 <+> 1-b-5-a-6 <+> 1-b-5-b-6
